;;; ob-axiom.el --- An org-babel backend for the axiom-environment system -*- lexical-binding: t -*-

;; Copyright (C) 2014 - 2019 Paul Onions

;; Author: Paul Onions
;; Keywords: Axiom, OpenAxiom, FriCAS
;; Package-Version: 20190623.2052
;; Package-Commit: 25932da0dc40c6877463cbf019b14ab05a6b0164

;; This file is free software, see the LICENCE file in this directory
;; for copying terms.

;; Package-Requires: ((emacs "24.2") (axiom-environment "20171021"))

;;; Commentary:

;; The ``ob-axiom'' package is an org-babel extension that integrates
;; the axiom-environment into org-mode, allowing a literate
;; development & presentation style with easy publishing to HTML, PDF,
;; etc.

;; See the ``Working with source code'' chapter of the Org manual for
;; details on using org-babel functionality in org files.

;; To enable this extension put
;;
;;  (require 'ob-axiom)
;;
;; in your ~/.emacs initialisation file.

;; There are two language names defined in this extension: ``axiom''
;; and ``spad''.  The former is for running arbitrary code in a
;; running axiom process, the latter for embedding SPAD source code
;; blocks, but both are declared as tangle'able languages.  They
;; correspond to ``axiom-input-mode'' and ``axiom-spad-mode'' in the
;; axiom-environment system, respectively.

;; The default behaviour of ``axiom'' source code blocks in ob-axiom
;; is that of ``:results output'' mode, i.e. scripting mode in the
;; terminology of the Org manual.  If this is unfamiliar to you, see
;; the ``Header arguments'' section of the ``Working with source
;; code'' chapter for more information on what this means.  By
;; default, all of the input line to Axiom, including the prompt, as
;; well as the output generated by Axiom, is echoed in the output
;; captured by Org.  To change this use the extra header options
;; explained below.

;; There are three extra header options (non-standard org-babel options)
;; for ``#+BEGIN_SRC axiom'' source code blocks:-
;;
;;   :block-read <yes/no/auto>  (defaults to auto)
;;   :show-prompt <yes/no>      (defaults to yes)
;;   :show-input <yes/no>       (defaults to yes)
;;
;; The block-read option forces ob-axiom to send the entire code block
;; to the running axiom process via a temporary file.  This allows
;; ``pile mode'' axiom source code to be handled correctly.  Otherwise
;; ob-axiom sends each line of the code block individually to the
;; axiom process for interpretation.

;; The show-prompt option allows to enable or inhibit the display of
;; the axiom REPL prompt on a block-by-block basis.

;; The show-input option allows to enable or inhibit the display of
;; the axiom input command/source on a block-by-block basis.

;; Finally, a useful tip for FriCAS.  If you want to suppress the
;; numbering of prompts and output results that FriCAS normally
;; generates, put the following at the start of your Org file:-

;; #+BEGIN_SRC axiom :results silent :exports none
;; )set message prompt plain
;; )boot $IOindex := nil
;; #+END_SRC

;;; Code:
(require 'ob)
(require 'ob-ref)
(require 'ob-comint)
(require 'ob-eval)

(require 'cl-lib)

(require 'axiom-environment)

;; Header arguments
(defconst org-babel-header-args:axiom
  '((block-read (no yes auto))
    (show-prompt (no yes))
    (show-input (no yes))))

(defvar org-babel-default-header-args:axiom
  '((:session . "Axiom Org-Babel Session")
    (:block-read . "auto")
    (:show-prompt . "yes")
    (:show-input . "yes")))

;; File extension for Axiom Input files
(add-to-list 'org-babel-tangle-lang-exts '("axiom" . "input"))

(add-to-list 'org-babel-tangle-lang-exts '("spad" . "spad"))

;; Configure org editing options
(add-to-list 'org-src-lang-modes '("axiom" . axiom-input))

(add-to-list 'org-src-lang-modes '("spad" . axiom-spad))

;;; Internal helper functions
;;;
(defun org-babel-axiom--starify-name (str)
  "Ensure valid process buffer name by wrapping with asterisks if necessary."
  (let ((name str))
    (unless (eql (aref str 0) ?*)
      (setq name (concat "*" name)))
    (unless (eql (aref str (1- (length str))) ?*)
      (setq name (concat name "*")))
    name))

;;; Org framework functions -- functions called by Org-mode
;;;
(defun org-babel-axiom-initiate-session (session params)
  "Start an Axiom session for use by org-babel."
  ;;(message "org-babel-axiom-initiate-session\n %S\n %S" session params)
  (unless (string= session "none")
    (let ((session-name (org-babel-axiom--starify-name session)))
      (let ((axiom-process-buffer-name session-name)) ; dynamic binding
        (if (org-babel-comint-buffer-livep session-name)
            session-name
          (axiom-process-start axiom-process-program))))))

(defun org-babel-axiom-var-to-axiom (val)
  "Convert an elisp var into a string of Axiom source code
specifying a var of the same value."
  ;;(message "org-babel-axiom-var-to-axiom\n %S" val)
  (if (listp val)
      (concat "[" (mapconcat #'org-babel-axiom-var-to-axiom val ", ") "]")
    (format "%S" val)))

(defun org-babel-variable-assignments:axiom (params)
  "Return a list of Axiom statements assigning the block's variables."
  ;;(message "org-babel-variable-assignments:axiom\n %S" params)
  (let ((vars (cl-mapcan (lambda (param)
                           (and (eql :var (car param))
                                (list (cdr param))))
                         params)))
    (mapcar
     (lambda (pair)
       (format "%S := %s" (car pair) (org-babel-axiom-var-to-axiom (cdr pair))))
     vars)))

(defun org-babel-expand-body:axiom (body params)
  "Expand BODY with PARAMS."
  ;;(message "org-babel-expand-body:axiom\n %S\n %S" body params)
  (mapconcat #'identity (append (org-babel-variable-assignments:axiom params)
                                (list body))
             "\n"))

(defun org-babel-execute:axiom (body params)
  "Execute a block of Axiom code with org-babel.
This function is called by `org-babel-execute-src-block'."
  ;;(message "org-babel-execute:axiom\n %S\n %S" body params)
  (let ((session (org-babel-axiom-initiate-session
                  (cdr (assoc :session params)) params))
        (block-read (cdr (assoc :block-read params))))
    (if (or (equal block-read "yes")
            (and (equal block-read "auto")
                 (org-babel-axiom--body-needs-block-read body)))
        (org-babel-axiom--execute-by-block-read session body params)
      (org-babel-axiom--execute-line-by-line session body params))))

(defun org-babel-axiom--body-needs-block-read (body)
  (string-match "^[[:space:]]+[[:graph:]]" body))

(defun org-babel-axiom--execute-by-block-read (session body params)
  (let* ((results-value (string-match "\\<value\\>" (cdr (assoc :results params))))
         (show-prompt (and (not results-value)
                           (equal (cdr (assoc :show-prompt params)) "yes")))
         (show-input (and (not results-value)
                          (equal (cdr (assoc :show-input params)) "yes")))
         (tmp-filename (make-temp-file "axiom" nil ".input")))
    (with-temp-buffer
      (insert (org-babel-expand-body:axiom body params))
      (write-region (point-min) (point-max) tmp-filename))
    (let ((axiom-process-buffer-name session)) ; dynamic binding
      (with-axiom-process-query-buffer
       (axiom-process-redirect-send-command
        (if show-input
            (format ")read %s" tmp-filename)
          (format ")read %s )quiet" tmp-filename))
        (current-buffer) nil nil t nil show-prompt)
       (let ((delete-trailing-lines t)) ; dynamic binding
         (delete-trailing-whitespace))
       (when results-value
         ;; remove type description
         (goto-char (point-max))
         (let ((pt (re-search-backward "^[[:space:]]*Type:" nil t)))
           (when pt
             (goto-char pt)
             (delete-region (point) (point-max)))))
       (buffer-substring (point-min) (point-max))))))

(defun org-babel-axiom--execute-line-by-line (session body params)
  (let* ((results-value (string-match "\\<value\\>" (cdr (assoc :results params))))
         (show-prompt (and (not results-value)
                           (equal (cdr (assoc :show-prompt params)) "yes")))
         (show-input (and (not results-value)
                          (equal (cdr (assoc :show-input params)) "yes")))
         (lines (split-string (org-babel-expand-body:axiom body params) "\n"))
         (axiom-process-buffer-name session)) ; dynamic binding
    (with-axiom-process-query-buffer
     (dolist (line lines)
       (when results-value
         (erase-buffer))
       (beginning-of-line)
       (unless (string-match "^[[:space:]]*$" line)
         (axiom-process-redirect-send-command
          line (current-buffer) nil t t show-input show-prompt)))
     (let ((delete-trailing-lines t)) ; dynamic binding
       (delete-trailing-whitespace))
     (when results-value
       ;; remove type description
       (goto-char (point-max))
       (let ((pt (re-search-backward "^[[:space:]]*Type:" nil t)))
         (when pt
           (goto-char pt)
           (delete-region (point) (point-max)))))
     (buffer-substring (point-min) (point-max)))))

(provide 'ob-axiom)

;;; ob-axiom.el ends here
